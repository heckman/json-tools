#!/usr/bin/env zsh
# Copyright 2024 Erik Ben Heckman <erik@heckman.ca>
# SPDX-License-Identifier: MIT


#  json v0.1


# Launch a json utility

# Usage: json <subcommand> [args]
#
# This is nothing more than a wrapper for utilities that start with "json-",
# which can be anywhere on the PATH.
#
# Subcommands should have the following header comment format:
# - The name of the program should be on the first comment line after blank lines.
#   (Comments before the first blank line are ignored.)
# - A short description should be on the next comment line.
# This script is an example of this format
#
# Subcommands cannot have spaces, tabs, or newlines in their names.

usage(){
	echo "usage: json <subcommand> [args]"
	echo
	echo "where subcommand is one of: "
	find "${(@)path}" -name 'json-*' 2>/dev/null |
		xargs basename |
		sort --unique |
		xargs which |
		xargs --max-args=1 awk '
			/^ *$/{ f=1 }
			f==0{ next } # skip until we see a blank line
			/^ *#/{
				if(subcommand==""){
					sub("^ *# *json-","")
					subcommand=$1
				}else{
					sub("^ *# *","")
					printf("%12s : %s\n",subcommand,$0)
					exit
				}
			}
		'
	echo
	echo "for help using a subcommand, try"
	echo "json <subcommand> --help"
	echo
}

if  (( $+commands[json-$1] ))
then
	exec "json-$1" "${@:2}" 2> >(
		sed "s/json-$1/json $1/" >&2
	)
else
	if [[ $# -eq 0 ]]
	then
		echo "Error: No subcommand specified." >&2
		exit_code=64;
	elif [[ $1 != '--help' && $1 != '-h' ]]
	then
		echo "Error: Can't find subcommand '$1'." >&2
		exit_code=69;
	fi
	echo >&2
	usage >&2
	exit ${exit_code:-0}
fi
